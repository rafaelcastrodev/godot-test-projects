##
##	WORLD CHUNK
##
##	Represents a single chunk of the world.
##	This node holds the TileMapLayers and other nodes for a specific
##	chunk coordinate. It is instantiated by the WorldChunkManager.
class_name WorldChunk
extends Node2D

# The ID of the TileSet source in the TileMap.
const TILE_SOURCE_ID: int = 0;

##@ Node References
@onready var ground_layer: TileMapLayer = $LayerGround; # TileMap layer for ground tiles
@onready var grass_layer: TileMapLayer = $LayerGrass; # TileMap layer for grass details
@onready var trees_layer: TileMapLayer = $LayerTrees; # TileMap layer for trees (and other props)
@onready var poi_layer: Node2D = $LayerPOI; # Node layer for instantiating POI scenes

##
##	Sets the Z-index for layers to ensure they render behind
##	the player and other entities.
##
func _ready() -> void:
	ground_layer.z_index = -1;
	grass_layer.z_index = -1;
#}


##
##	Populates the chunk's layers based on the data
##	generated by the WorldChunkManager.
##
##	This function is called immediately after the chunk is instantiated.
##
func initialize(chunk_data: Dictionary) -> void:

	## -- PLACE GRASS --
	# Iterate through the grass data and place each tile.
	if chunk_data.grass_tiles:
		for coords in chunk_data.grass_tiles:
			grass_layer.set_cell(
				coords, # Local tile coordinates (e.g., 0,0 to 31,31)
				TILE_SOURCE_ID,
				chunk_data.grass_tiles[coords] # Atlas coordinates (e.g., 1,0)
			);
	#}

	## -- PLACE TREES --
	# Iterate through the tree data and place each tile.
	if chunk_data.trees_tiles:
		for coords in chunk_data.trees_tiles:
			trees_layer.set_cell(
				coords,
				TILE_SOURCE_ID,
				chunk_data.trees_tiles[coords]
			);
	#}

	## -- PLACE POI (Points of Interest) --
	# Iterate through the POI data, load the scene,
	# and instantiate it as a child of the POI layer.
	if not chunk_data.poi.is_empty():
		for poi in chunk_data.poi:
			var poi_scene: PackedScene = load(poi.scene);
			var poi_instance = poi_scene.instantiate();
			poi_layer.add_child(poi_instance);
			# Position is set relative to the chunk's origin
			poi_instance.position = poi.coords;
	#}
#}
